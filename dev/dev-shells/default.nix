{
  lib,
  pkgs,
  projectRoot,
  ...
}:
let
  inherit (lib)
    attrNames
    baseNameOf
    concatMap
    concatStringsSep
    elem
    escapeShellArg
    foldl'
    getExe
    map
    mapAttrs
    optional
    removeAttrs
    ;

  inherit (lib.generators)
    toINIWithGlobalSection
    ;

  inherit (pkgs)
    git
    mkShell
    writeText
    ;

  mkFile = request: request.engine request;

  install =
    request:
    let
      source = escapeShellArg (
        toString (mkFile request)
      );

      output = escapeShellArg request.output;
    in
    ''
      target="$projectRoot"/${output}
      mkdir -p "$(dirname -- "$target")"
      ln -sfn ${source} "$target"
    '';

  hookExtra =
    request:
    if request ? hook && request.hook ? extra then
      request.hook.extra request.data
    else
      "";

  installWithHook =
    request:
    let
      extra = hookExtra request;
    in
    concatStringsSep "\n" (
      [
        (install request)
      ]
      ++ optional (extra != "") extra
    );

  addFile =
    names: name:
    if elem name names then
      names
    else
      (foldl' addFile names (
        files.${name}.depends or [ ]
      ))
      ++ [
        name
      ];

  toml =
    request:
    let
      inherit (request)
        data
        output
        ;
    in
    (pkgs.formats.toml { }).generate
      (baseNameOf output)
      data;

  files = {
    editorconfig = {
      data = {
        root = true;

        "*" = {
          charset = "utf-8";
          end_of_line = "lf";
          indent_size = 8;
          indent_style = "tab";
          insert_final_newline = true;
          max_line_length = 70;
          tab_width = 8;
        };

        "*.el" = {
          indent_style = "space";
          indent_size = "unset";
          tab_width = 2;
        };

        "*.md" = {
          indent_size = 2;
          indent_style = "space";
          max_line_length = 80;
          trim_trailing_whitespace = false;
        };

        "*.org" = {
          indent_size = 2;
          indent_style = "space";
          max_line_length = 80;
          trim_trailing_whitespace = false;
        };

        "*.nix" = {
          indent_style = "space";
          max_line_length = 80;
          tab_width = 2;
        };
      };

      engine =
        request:
        let
          inherit (request)
            data
            output
            ;

          name = baseNameOf output;

          value = {
            globalSection = {
              root = data.root or true;
            };

            sections = removeAttrs data [
              "root"
            ];
          };
        in
        writeText name (toINIWithGlobalSection { } value);

      output = ".editorconfig";

      packages = with pkgs; [
        editorconfig-checker
      ];
    };

    prek = {
      data = {
        default_install_hook_types = [
          "pre-commit"
        ];

        repos = [
          {
            repo = "local";

            hooks = [
              {
                entry = "treefmt --fail-on-change";
                id = "treefmt";
                language = "system";
                name = "treefmt";

                stages = [
                  "pre-commit"
                ];
              }
            ];
          }
        ];
      };

      depends = [
        "treefmt"
      ];

      engine = toml;

      hook = {
        extra =
          cfg:
          let
            inherit (pkgs)
              prek
              runtimeShell
              writeScript
              ;

            mkInstall = stage: ''
              if gitDir="$(${getExe git} -C "$projectRoot" rev-parse --absolute-git-dir 2>/dev/null)"; then
                mkdir -p "$gitDir/hooks"
                ln -sf "${mkScript stage}" "$gitDir/hooks/${stage}"
              fi
            '';

            mkScript =
              stage:
              writeScript "prek-${stage}" ''
                #!${runtimeShell}
                if [ "''${PREK:-}" = "0" ] ; then
                  exit 0
                fi

                repoRoot="$(${getExe git} rev-parse --show-toplevel 2>/dev/null || true)"

                if [ -z "$repoRoot" ]; then
                  repoRoot="$PWD"
                fi

                gitDir="$(${getExe git} -C "$repoRoot" rev-parse --absolute-git-dir 2>/dev/null || true)"

                if [ -n "$gitDir" ]; then
                  if [ -e "$gitDir/MERGE_HEAD" ] \
                    || [ -d "$gitDir/rebase-apply" ] \
                    || [ -d "$gitDir/rebase-merge" ]; then
                    exit 0
                  fi

                  ref="$(${getExe git} -C "$repoRoot" symbolic-ref --quiet --short HEAD 2>/dev/null || true)"

                  if [ "$ref" = "update_flake_lock_action" ]; then
                    exit 0
                  fi
                fi

                exec ${getExe prek} -C "$repoRoot" run --stage "${stage}" "$@"
              '';
          in
          concatStringsSep "\n" (
            map mkInstall cfg.default_install_hook_types
          );
      };

      output = "prek.toml";

      packages = with pkgs; [
        git
        prek
      ];
    };

    treefmt = {
      data = {
        formatter = {
          emacs-lisp = {
            command = "elisp-format";

            includes = [
              "*.el"
            ];
          };

          markdown = {
            command = "mdformat";

            includes = [
              "*.md"
            ];

            options = [
              "--extensions=frontmatter"
              "--wrap=80"
            ];
          };

          nix = {
            command = "nixfmt";

            includes = [
              "*.nix"
            ];

            options = [
              "--width=50"
            ];
          };
        };
      };

      engine = toml;
      output = "treefmt.toml";

      packages =
        let
          mdformatWithPlugins = pkgs.mdformat.withPlugins (
            ps: with ps; [
              mdformat-frontmatter
            ]
          );
        in
        with pkgs;
        [
          elisp-format
          mdformatWithPlugins
          nixfmt
          treefmt
        ];
    };
  };

  names = foldl' addFile [ ] (attrNames files);
in
mkShell {
  packages =
    (concatMap (
      name: files.${name}.packages or [ ]
    ) names)
    ++ (with pkgs; [
      git
    ]);

  shellHook = concatStringsSep "\n" (
    [
      ''projectRoot="$(${getExe git} rev-parse --show-toplevel)"''
    ]
    ++ map (name: installWithHook files.${name}) names
  );

  passthru = {
    dependencies = mapAttrs (_: request: {
      packages = request.packages or [ ];
    }) files;

    files = mapAttrs (_: mkFile) files;
  };
}
